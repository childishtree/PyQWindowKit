# =========================================================
#
# CMake configuration for PyQWindowKit
# This project is used to generate Python bindings for the QWindowKit library using Shiboken6.
# 前置条件: - 必需激活Python虚拟环境，并安装以下依赖：
#               pip install shiboken6_generator PySide6 shiboken6
#          - 必需使用release或其他非debug配置进行构建
#          - QWindowKit已构建并安装在项目的install目录中
# =========================================================

cmake_minimum_required(VERSION 3.20)

project(PyQWindowKit)

# =================== No DEBUG Model ===========================
string(TOUPPER "${CMAKE_BUILD_TYPE}" BUILD_TYPE_UPPER)
string(TOUPPER "${CMAKE_CONFIGURATION_TYPES}" CONFIG_TYPES_UPPER)

# Only prohibit Debug if it is explicitly selected as CMAKE_BUILD_TYPE
if(CMAKE_BUILD_TYPE AND BUILD_TYPE_UPPER STREQUAL "DEBUG")
    message(FATAL_ERROR "[ERROR] Debug build is prohibited! Please build with Release/RelWithDebInfo or other non-Debug configurations.")
endif()

# ========= 0. Setup Python Venv  ============
set(Python_EXECUTABLE "${CMAKE_CURRENT_SOURCE_DIR}/.venv/Scripts/python.exe")

# ====================== 1. Find Qt6 ======================
find_package(Qt6 COMPONENTS Core Gui Widgets Quick REQUIRED)
set(QT_COMPONENTS Core Gui Widgets)     # 可选Quick

# ====================== 2. Find QWindowKit ======================
list(APPEND CMAKE_PREFIX_PATH "${CMAKE_CURRENT_SOURCE_DIR}/install")
set(QWK_BUILD_QUICK TRUE)
set(QWK_COMPONENTS Core Widgets)

if(QWK_BUILD_QUICK)
    list(APPEND QWK_COMPONENTS Quick)
    list(APPEND QT_COMPONENTS Quick Network OpenGL Qml)
endif()

find_package(QWindowKit COMPONENTS Core Widgets REQUIRED)

# ====================== 3. Python/Shiboken6 ======================
find_package(Python COMPONENTS Interpreter Development REQUIRED)
execute_process(
    COMMAND ${Python_EXECUTABLE} -c 
    # "import site; print(next(p for p in site.getsitepackages() if 'site-packages' in p))"
    # OUTPUT_VARIABLE Python_SITELIB
    "import os, shiboken6_generator; print(os.path.dirname(shiboken6_generator.__file__), end='')"
    OUTPUT_VARIABLE SHIBOKEN6_GENERATOR_DIR
    RESULT_VARIABLE SHIBOKEN6_GENERATOR_FOUND
    OUTPUT_STRIP_TRAILING_WHITESPACE
)

if(NOT "${SHIBOKEN6_GENERATOR_FOUND}" EQUAL "0")
    message(FATAL_ERROR "Could not find 'shiboken6_generator'. Run: pip install shiboken6_generator")
endif()

execute_process(
    COMMAND "${Python_EXECUTABLE}" -c 
    "import os, PySide6; print(os.path.dirname(PySide6.__file__), end='')"
    OUTPUT_VARIABLE PYSIDE6_DIR
    OUTPUT_STRIP_TRAILING_WHITESPACE
)

file(TO_CMAKE_PATH "${SHIBOKEN6_GENERATOR_DIR}" SHIBOKEN6_GENERATOR_DIR)
file(TO_CMAKE_PATH "${PYSIDE6_DIR}" PYSIDE6_DIR)
list(APPEND CMAKE_PREFIX_PATH "${SHIBOKEN6_GENERATOR_DIR}" "${PYSIDE6_DIR}")
find_package(Shiboken6Tools REQUIRED)

# list(APPEND CMAKE_PREFIX_PATH "${Python_SITELIB}/shiboken6_generator/lib/cmake")
# find_package(Shiboken6Tools REQUIRED)



# ====================== 4. Bindings Configuration ======================
set(packahe_name "PyQWindowKit")
set(origin_library "QWindowKit::Widgets")
set(bindings_library "Widgets")
set(wrapped_header ${CMAKE_SOURCE_DIR}/bindings_widgets.h)
set(typesystem_file ${CMAKE_SOURCE_DIR}/bindings_widgets.xml)
set(generated_sources
    # wrapper命名规则与typesystem_xml一致, xxx_module_wrapper.cpp中的xxx与xml中的一致
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/widgets_module_wrapper.cpp   
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwk_widgetwindowagent_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwk_windowagentbase_wrapper.cpp
)

# ====================== 6. Generate Bindings ======================
shiboken_generator_create_binding(
    EXTENSION_TARGET ${bindings_library}         # 生成python扩展模块的名称
    GENERATED_SOURCES ${generated_sources}       # Shiboken自动生成的C++包装器文件
    HEADERS ${wrapped_header}                    # 生成依赖的C++头文件
    TYPESYSTEM_FILE ${typesystem_file}           # Shiboken类型系统文件，定义那些C++类/函数需要生成绑定、如何让处理类型映射
    LIBRARY_TARGET ${origin_library}             # 包绑定的C++库
    FORCE_LIMITED_API                            # 强制使用Python的有限API，提升兼容性和稳定性（兼容更多的python版本）
    QT_MODULES ${QT_COMPONENTS}                  # 指定“LIBRARY_TARGET”依赖的Qt模块
    SHIBOKEN_EXTRA_OPTIONS "-I${CMAKE_CURRENT_SOURCE_DIR}/install/include" 
                           "-I${CMAKE_CURRENT_SOURCE_DIR}/install/include/QWindowKit"
)

# ====================== 7. Install ======================
target_sources(${bindings_library} PRIVATE ${generated_sources})
install(TARGETS ${bindings_library} DESTINATION ${packahe_name})


if(QWK_BUILD_QUICK)
    set(origin_library "QWindowKit::Quick")
    set(bindings_library "Quick")
    set(wrapped_header ${CMAKE_SOURCE_DIR}/bindings_quick.h)
    set(typesystem_file ${CMAKE_SOURCE_DIR}/bindings_quick.xml)
    set(generated_sources
        ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/quick_module_wrapper.cpp
        ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwk_quickwindowagent_wrapper.cpp
        ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/qwk_windowagentbase_wrapper.cpp
    )

    shiboken_generator_create_binding(
        EXTENSION_TARGET ${bindings_library}
        GENERATED_SOURCES ${generated_sources}
        HEADERS ${wrapped_header}
        TYPESYSTEM_FILE ${typesystem_file}
        LIBRARY_TARGET ${origin_library}
        FORCE_LIMITED_API
        QT_MODULES ${QT_COMPONENTS}
        SHIBOKEN_EXTRA_OPTIONS "-I${CMAKE_CURRENT_SOURCE_DIR}/install/include"
                               "-I${CMAKE_CURRENT_SOURCE_DIR}/install/include/QWindowKit"
    )

    target_sources(${bindings_library} PRIVATE ${generated_sources})
    install(TARGETS ${bindings_library} DESTINATION ${packahe_name})
endif()

# install(FILES "${CMAKE_CURRENT_SOURCE_DIR}/${bindings_library}/__init__.pyi" DESTINATION ${bindings_library})


# 2. Install QWindowKit dependency DLLs (Release versions only)
foreach(component ${QWK_COMPONENTS})
    set(target_name "QWindowKit::${component}")
    get_target_property(dll_path ${target_name} IMPORTED_LOCATION_RELEASE)
    
    if(dll_path)
        install(FILES "${dll_path}" DESTINATION ${packahe_name})
    else()
        message(WARNING "Could not find Release DLL for ${target_name}")
    endif()
endforeach()


# 执行python脚本，生成xxx.pyi和__init__.py文件
set(MODULE_ARGS "\"Widgets\"")
if(QWK_BUILD_QUICK)
    set(MODULE_ARGS "${MODULE_ARGS} \"Quick\"")
endif()

install(CODE "
    execute_process(
        COMMAND \"${Python_EXECUTABLE}\" \"${CMAKE_CURRENT_SOURCE_DIR}/scripts/generate_init.py\"
        \"\${CMAKE_INSTALL_PREFIX}\" \"${packahe_name}\" ${MODULE_ARGS}
    )
")

